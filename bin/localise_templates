#!/usr/bin/env perl

use strict;
use warnings;
use Template;
use mySociety::Locale;

sub get_langs {
    my $lang_list = 'locale/lang_list';
    my @langs = do {
        open my $f, '<', $lang_list
            or die "Can't open $lang_list: $!\n";
        <$f>;
    };

    return @langs;
}

sub loc {
    my @args = @_;
    return _(@args);
}

sub tprintf {
    my ( $format, @args ) = @_;
    @args = @{$args[0]} if ref $args[0] eq 'ARRAY';
    return sprintf $format, @args;
}


my $vars = {
    loc => \&loc,
    tprintf => \&tprintf,
};

for my $lang ( get_langs() ) {
    chomp $lang;
    my @lang_parts = split(',', $lang);

    mySociety::Locale::negotiate_language($lang);
    mySociety::Locale::gettext_domain('FixMyStreetMobileApp');
    mySociety::Locale::change($lang_parts[0]);

    my $t = Template->new({
        INCLUDE_PATH => 'templates',
        OUTPUT_PATH  => 'compiled/' . $lang_parts[0]
    });

    for my $file ( glob('templates/*') ) {
        $file =~ s%templates/%%;
        $t->process( $file, $vars, $file ) or warn $t->error;
    }
}
